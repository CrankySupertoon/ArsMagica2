buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
	}
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.matthewprenger:CurseGradle:1.0-SNAPSHOT'
    }
}
repositories {
    maven {
        url = "http://dvs1.progwml6.com/files/maven"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext.configFile = file('build.properties')
ext.config = parseConfig(configFile)

version = "${config.version}-${config.build_number}"
group= "am2" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ArsMagica2"

minecraft {
    version = "1.12.2-14.23.2.2654"
    runDir = "run"
    replace 'GRADLE:BUILD', config.build_number
    replace 'GRADLE:VERSION', config.version
    replaceIn 'ArsMagica2.java'
	//replace '${version}', project.version
    mappings = "snapshot_20171003"  //uncomment this line to use newest Forge mappings. Needs a lot of rework.
}

jar {
    manifest {
        attributes 'FMLCorePlugin' : 'am2.common.asm.Preloader'
        attributes 'FMLCorePluginContainsFMLMod' : 'true'
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_${config.mcversion}:${config.jei_version}:api"
    runtime "mezz.jei:jei_${config.mcversion}:${config.jei_version}"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task incrementBuildNumber(dependsOn: 'reobfJar') {
    doLast {
        config.build_number = (config.build_number.toString().toInteger()) + 1
        configFile.withWriter {
            config.toProperties().store(it, "")
        }
    }
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

jar {
    archiveName = "${baseName}-${version}.jar"
}

defaultTasks 'clean', 'build', 'incrementBuildNumber'
